
buildscript {
    repositories {
        mavenCentral()
        jcenter()
        google()
        maven {
            url "https://jitpack.io"
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.3'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'eclipse'
apply plugin: 'idea'

repositories {
    mavenCentral()
    jcenter()
    google()
    maven {
        url "https://jitpack.io"
    }
    flatDir {
        dirs 'libs'
    }
}

android {
    compileSdkVersion 29

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        project.file("exclude.txt").eachLine {
            line ->
                println line
                if (line && line.trim() && !line.startsWith("#")) {
                    exclude line.trim()
                }
        }
    }


    lintOptions {
        abortOnError false
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
    }

    if (System.getenv("KEYSTORE") != null) {
        signingConfigs {
            release {
                storeFile file(System.getenv("KEYSTORE"))
                storePassword System.getenv("STORE_PASSWORD")
                keyPassword System.getenv("KEY_PASSWORD")
                if (keyPassword == null) {
                    keyPassword = storePassword
                }
                keyAlias "aard2-android"
            }
        }
    }

    buildTypes {
        if (System.getenv("KEYSTORE") != null) {
            release {
                signingConfig signingConfigs.release
            }
        }
    }

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 29
    }

}

dependencies {
    api 'com.fasterxml.jackson.core:jackson-databind:2.4+'
    api 'com.fasterxml.jackson.core:jackson-core:2.4+'
    api 'com.fasterxml.jackson.core:jackson-annotations:2.4+'
    api 'com.github.kazy1991:FontDrawable:0.9.5'
    api project(':slobj')
    api project(':slobber')
    api project(':slobber').files('libs/simple-5.1.7.1.itkach.jar')
}
