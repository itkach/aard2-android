
buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.3'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'eclipse'
apply plugin: 'idea'

sourceCompatibility = '1.7'

repositories {
    mavenCentral()
    flatDir {
        dirs 'libs'
    }
}

android {
    compileSdkVersion 19
    buildToolsVersion "21.1.2"

    defaultConfig {
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
        multiDexEnabled true
    }

    configurations {
        all*.exclude group: 'com.android.support', module: 'support-annotations'
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        project.file("exclude.txt").eachLine {
            line ->
                println line
                if (line && line.trim() && !line.startsWith("#")) {
                    exclude line.trim()
                }
        }
    }


    lintOptions {
        abortOnError false
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
    }

    if (System.getenv("KEYSTORE") != null) {
        signingConfigs {
            release {
                storeFile file(System.getenv("KEYSTORE"))
                storePassword System.getenv("STORE_PASSWORD")
                keyPassword System.getenv("KEY_PASSWORD")
                if (keyPassword == null) {
                    keyPassword = storePassword
                }
                keyAlias "aard2-android"
            }
        }
    }

    buildTypes {
        if (System.getenv("KEYSTORE") != null) {
            release {
                signingConfig signingConfigs.release
            }
        }
    }
}

dependencies {
    compile 'com.fasterxml.jackson.core:jackson-databind:2.4+'
    compile 'com.fasterxml.jackson.core:jackson-core:2.4+'
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.4+'
    compile 'com.shamanland:fonticon:0.1.8'
    compile project(':slobj')
    compile project(':slobber')
    compile project(':slobber').files('libs/simple-5.1.7.1.itkach.jar')
    compile files('libs/android-support-v13.jar')
    compile 'junit:junit:4.12'
    compile 'com.android.support.test:runner:0.5'
    testCompile 'org.robolectric:robolectric:2.2'
    testCompile 'org.mockito:mockito-core:1.10.19'
    androidTestCompile 'junit:junit:4.12'
}
